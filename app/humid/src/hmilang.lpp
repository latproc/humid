/*
	All rights reserved. Use of this source code is governed by the
	3-clause BSD License in LICENSE.txt.
*/
%{
	#include <stdlib.h>
	#include "hmilang.h"
	#include "hmilang.tab.hpp"
	#include "linkableobject.h"

	void yyerror(const char *);

	static char *buf;
	static int buflen = 0;
	static char *bufp;
	extern YYSTYPE yylval;
	int yycharno;

	void append_char(int ch);
%}

%x STRING
%x COMMENT
%x HASHCOMMENT
%x PATTERNSTR
%x PLUGIN_CODE

%%

\n              { yylineno++; yycharno = 1; }

"<<"                return CONCAT;
"~"                 return MATCHES;
16BIT               return tokWORD;
32BIT               return tokDOUBLEWORD;
ABORT				return ABORT;
action			return ACTION;
ALL                 return ALL;
AND                 return AND;
ANY                 return ANY;
ARE                 return ARE;
ASSIGN              return ASSIGN;
AT                  return AT;
BECOMES             return BECOMES;
BETWEEN             return BETWEEN;
BITSET              return BITSET;
BY                  return BY;
CALL				return CALL;
CATCH				return CATCH;
CHANNEL             return CHANNEL;
CLASS				return CLASS;
CLEAR               return CLEAR;
COMMAND				return COMMAND;
COMMANDS			return COMMANDS;
COMBINATION         return COMBINATION;
COMMON              return COMMON;
CONDITION           return CONDITION;
CONSTRAINT			return CONSTRAINT;
COPY                return COPY;
COUNT               return COUNT;
CREATE              return CREATE;
DEC                 return DEC;
DEFAULT             return DEFAULT;
DEFAULTS            return DEFAULTS;
DIFFERENCE          return tokDIFFERENCE;
DISABLE             return DISABLE;
DISABLED            return DISABLED;
DURING              return DURING;
ELSE				return ELSE;
ENABLE              return ENABLE;
ENABLED             return ENABLED;
ENTER               return ENTER;
ENTRIES             return ENTRIES;
ERROR				return tokERROR;
ERRORS				return ERRORS;
EXECUTE				return EXECUTE;
EXISTS				return EXISTS;
EXPORT              return EXPORT;
EXPORTS             return EXPORTS;
EXTENDS             return EXTENDS;
EXTRACT             return EXTRACT;
FIRST               return FIRST;
FROM				return FROM;
GLOBAL              return GLOBAL;
GROUP               return tokGROUP;
IGNORE				return tokIGNORE;
IGNORES				return IGNORES;
INC                 return INC;
INCLUDE             return INCLUDE;
INCLUDES            return INCLUDES;
INTERFACE           return tokINTERFACE;
ITEM                return ITEM;
ITEMS               return ITEMS;
IDENTIFIER          return IDENTIFIER;
LEAVE               return LEAVE;
LINKED				return LINKED;
IF					return IF_;
IN                  return tokIN;
INITIAL             return INITSTATE;
IS                  return EQ;
IS[ ]*NOT           return NE;
KEY                 return KEY;
LAST                return LAST;
LENGTH              return LENGTH;
LINK								return LINK;
LOCK				return LOCK;
LOG                 return LOG;
MACHINE             return STATEMACHINE;
MACHINES            return MACHINES;
MATCHES             return MATCHES;
MATCHING            return MATCHING;
MODBUS				return MODBUS;
MODULE              return MODULE;
MODULES             return MODULES;
MONITORS            return MONITORS;
MOVE                return MOVE;
NAME                return NAME;
NOT                 return NOT;
OF                  return OF;
ON					return ON;
OPTION              return OPTION;
OR                  return OR;
PLUGIN              return PLUGIN;
LOCAL				return LOCAL;
PROPERTIES          return PROPERTIES;
PROPERTY			return PROPERTY;
PUBLISHER			return tokPUBLISHER;
PUSH				return PUSH;
READONLY            return READONLY;
READWRITE           return READWRITE;
REMOTE							return REMOTE;
REPLACE             return REPLACE;
REPORTS				return REPORTS;
PRIVATE				{
	char buf[100];
	snprintf(buf, 100, "Error at line %d PRIVATE semantics are changing. Use LOCAL instead\n", yylineno);
	yyerror(buf);
	return LOCAL;
}
PROPERTY_CHANGES	return PROPERTY_CHANGES;
RO                  return READONLY;
ROUTE               return ROUTE;
RW                  return READWRITE;
RECEIVE             return RECEIVE;
RECEIVES            return RECEIVES;
REQUIRES            return REQUIRES;
RETURN				return RETURN;
RESUME              return RESUME;
SCREEN				return SCREEN;
SDO					return SDO;
SELECT              return SELECT;
SEND                return SEND;
SENDS               return SENDS;
SET                 return SET;
SIZE                return tokSIZE;
SHARES              return SHARES;
SHUTDOWN			return SHUTDOWN;
SORT                return SORT;
STATE               return STATE;
STATE_CHANGES		return STATE_CHANGES;
STATES              return STATES;
STRUCTURE			return STRUCTURE;
STRING              return STRINGTYPE;
TAG					return TAG;
TAKE                return TAKE;
THROTTLE			return THROTTLE;
THROW				return THROW;
TIMEOUT				return TIMEOUT;
TO                  return TO;
TRANSITION          return TRANSITION;
UNLOCK				return UNLOCK;
UPDATES             return UPDATES;
VERSION             return VERSION;
USING               return USING;
WAIT                return WAIT;
WAITFOR             return WAITFOR;
WHEN                return WHEN;
WHERE               return WHERE;
WITH                return WITH;
WITHIN              return WITHIN;

[A-Za-z][A-Za-z0-9_.]*       {
	yylval.sVal = strdup(yytext);
	return SYMBOL;
}

"0x"[0-9a-fA-F][0-9a-fA-F]*	{
	long val;
	char *p;
	val = strtol(yytext, &p, 16);
	yylval.iVal = val;
	return INTEGER;
}

0\.0 {
	yylval.fVal = atof(yytext);
	return FLOATVAL;
}

0\. {
	yylval.fVal = atof(yytext);
	return FLOATVAL;
}

[-]{0,1}[0-9][0-9]*[.][0-9]* {
	yylval.fVal = atof(yytext);
	return FLOATVAL;
}

[-]{0,1}[0-9][0-9]*[.][0-9]*E[0-9]{1,3} {
	yylval.fVal = atof(yytext);
	return FLOATVAL;
}

0           {
	yylval.iVal = atoi(yytext);
	return INTEGER;
}

[-]{0,1}[1-9][0-9]* {
	yylval.iVal = atoi(yytext);
	return INTEGER;
}

"%BEGIN_PLUGIN"			BEGIN PLUGIN_CODE;
<PLUGIN_CODE>"%END_PLUGIN"	BEGIN 0;
<PLUGIN_CODE>\n		{yylineno++; yycharno=1; }
<PLUGIN_CODE>.		;

"/*"			BEGIN COMMENT;
<COMMENT>"*/"	BEGIN 0;
<COMMENT>\n		{yylineno++; yycharno=1; }
<COMMENT>.		;

"#"             BEGIN HASHCOMMENT;
<HASHCOMMENT>\n     { yylineno++; yycharno=1; BEGIN 0; }
<HASHCOMMENT>.		;

`               {
	BEGIN PATTERNSTR;
	if (!buf)
	{
		buf = (char *)malloc(20);
		buflen = 20;
	}
	bufp = buf;
	*bufp = 0;
}
<PATTERNSTR>\\`    { append_char('`'); }
<PATTERNSTR>`      { BEGIN 0; yylval.sVal = buf; buf = NULL; return tokPATTERN; }
<PATTERNSTR>\\[0-3][0-7][0-7] { unsigned char ch = ((yytext[1]-'0')*8 + yytext[2]-'0')*8+yytext[3]-'0'; append_char(ch);  }
<PATTERNSTR>\r		{ append_char('\r');  }
<PATTERNSTR>\t		{ append_char('\t');}
<PATTERNSTR>\n		{ append_char('\n'); yylineno++; yycharno=1; }
<PATTERNSTR>.      { append_char( *yytext); }

\"			    {  /*"*/
BEGIN STRING;
if (!buf)
{
	buf = (char *)malloc(20);
	buflen = 20;
}
bufp = buf;
*bufp = 0;
}
<STRING>\\n		{ append_char('\n'); }
<STRING>\\r		{ append_char('\r'); }
<STRING>\\t		{ append_char('\t'); }
<STRING>\\[0-3][0-7][0-7] { unsigned char ch = ((yytext[1]-'0')*8 + yytext[2]-'0')*8+yytext[3]-'0'; append_char(ch); }
<STRING>\\\"	{ /*"*/ append_char('\"'); }
<STRING>\"		{ /*"*/ BEGIN 0; yylval.sVal = buf; buf = NULL; return STRINGVAL; }
<STRING>\n		{ append_char('\n'); yylineno++; yycharno=1; }
<STRING>.		{ append_char(*yytext); }

[-()=+*/;{}.%] {
	return *yytext;
}

"["             return BEGINPROP;
"]"             return ENDPROP;
":"             return PROPSEP;
","             return COMMA;
">="            return GE;
"<="            return LE;
"<"             return LT;
">"             return GT;
"=="            return EQ;
"!="            return NE;
"&&"            return AND;
"||"            return OR;
":="            return BECOMES;

"&"             return *yytext;
"|"             return *yytext;
"^"             return *yytext;
"!"             return *yytext;

[ \t]+        ;       /* ignore whitespace */

\'              /*' ignore single quotes */
.               { printf("found char %d (%c)\n", *yytext, *yytext); yyerror("Unknown character"); }

%%
int yywrap(void) {
	return 1;
}

void append_char(int ch)
{
	if (bufp - buf == buflen-1) {
		buf = (char *)realloc(buf, buflen + 20);
		bufp = buf + buflen-1;
		buflen += 20;
	}
	*bufp++ = ch;
	*bufp = 0;
}
